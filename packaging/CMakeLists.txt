set(CPACK_PACKAGE_NAME "NSEW")
set(CPACK_PACKAGE_INSTALL_DIRECTORY "${CPACK_PACKAGE_NAME}")

set(CPACK_PACKAGE_VENDOR "dextercd#7326")

set(CPACK_ARCHIVE_COMPONENT_INSTALL ON)
set(CPACK_COMPONENTS_GROUPING ALL_COMPONENTS_IN_ONE)
set(CPACK_INCLUDE_TOPLEVEL_DIRECTORY OFF)
set(CPACK_GENERATOR ZIP)

set(CPACK_PACKAGE_FILE_NAME "${PROJECT_NAME}-${PROJECT_VERSION}")

if (NOT DEFINED CPACK_COMPONENTS_ALL)
    get_cmake_property(CPACK_COMPONENTS_ALL COMPONENTS)
    list(REMOVE_ITEM CPACK_COMPONENTS_ALL
        NSEW_Client NSEW_Server
    )
endif()

if (NSEW_INCLUDE_EXAMPLE)
    list(APPEND CPACK_INSTALL_CMAKE_PROJECTS
        ${CMAKE_BINARY_DIR} ${CMAKE_PROJECT_NAME} NSEW_Client /
        ${CMAKE_BINARY_DIR} ${CMAKE_PROJECT_NAME} NSEW_Server /
        ${CMAKE_BINARY_DIR} ${CMAKE_PROJECT_NAME} NSEW_Library /examples/nsew_client/deps
    )

    foreach(component IN LISTS CPACK_COMPONENTS_ALL)
        list(APPEND CPACK_INSTALL_CMAKE_PROJECTS
            ${CMAKE_BINARY_DIR} ${CMAKE_PROJECT_NAME} "${component}" /
        )
    endforeach()
endif()

set(CPACK_VERBATIM_VARIABLES ON)
include(CPack)
